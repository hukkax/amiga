; -----------------------------------------------------------------------------
; VoidBoot2: a simple bootblock for Amiga OCS/AGA (vasm)
; by hukka/void Mar 2020
; -----------------------------------------------------------------------------

RELEASE        = 0	; generate final bootblock?
NOAUDIO        = 0	; silent mode?
SNARE          = 0	; crappy "snare" on the bass channel?
ORIGINALMELODY = 0	; use the original melody sample
NOINTROPATTERN = 0	; skip the bass+"drums" section at start
ALTPALETTE     = 0	; alternative color palette?

; -----------------------------------------------------------------------------

	machine 68000

	if	RELEASE = 0

	opt	a+,o+,l+				; optimize
	opt	ob+,oc+,od+,og+,oj+,ol+,om+,on+,ox+	; vasm specific

	include	"startup.asm"

	else

	opt	a+,o+,p+				; optimize
	opt	ob+,oc+,od+,og+,oj+,ol+,om+,on+,ox+	; vasm specific
	;opt	ow+					; show optimizations

	endc

	include "custom.i"

; -----------------------------------------------------------------------------

	macro push
	movem.l \1, -(sp)
	endm

	macro pop
	movem.l (sp)+, \1
	endm

	macro echosize
.\@	echo	\1
	printv	.\@ - \2
	endm

	macro printsize
.\@	printv	.\@ - \1
	endm

; -----------------------------------------------------------------------------

	if	RELEASE = 1
_LVOFindResident= -96
gb_ActiView	= 34
gb_copinit	= 38
	endc

SCREENWIDTH	= 320
SCREENHEIGHT	= 256
SCREENDEPTH	= 2		; number of bitplanes
YPADDING	= 40

CC		= $FF		; sets note volume back to maximum
ENDBYTE		= $FE		; end marker for nybblepacked data

SONGTICKS	= 5		; song tempo, higher=slower
PATTERNSIZE	= 128*2
FADESPEED	= 1		; melody instrument fade speed (higher=faster)
FADESPEED_BASS	= 4		; bass instrument fade speed

	if NOINTROPATTERN = 1
VOLUME_BASS	= 64
VOLUME_MELODY	= $28
	else
VOLUME_BASS	= 64
VOLUME_MELODY	= 64
	endc

MAXSONGLOOPS	= 6
S_VOL		= 0		; volume
S_FADEY		= 1		; fade yes
S_FADEN		= 2		; fade no
CHANSIZE	= 2		; channel struct length
CHANNELS_ON	= (DMAF_SETCLR!DMAF_AUD0!DMAF_AUD1!DMAF_AUD2!DMAF_AUD3!DMAF_MASTER)

	SECTION BBLK, CODE

; -----------------------------------------------------------------------------
; Testing wrapper - for running bootblock during development
;
	if	RELEASE = 0

Main:	bsr	Init			; Call the bootblock

Quit:	lea	CUSTOM, a6
	move.w	#$8020, DMACON(a6)	; re-enable sprite DMA
	moveq	#0, d0			; errorcode=0
	rts

	endc

*******************************************************************************
; The bootblock starts here. It cannot generally be assumed to be placed in a
; specific kind of memory, or at any specific address, so all code must be
; PC relative, and all chip data must be explicitly copied to chip memory.
; -----------------------------------------------------------------------------
; A4 = ChipSpace
; A6 = Custom

BB_START:
	DC.B	'DOS',0
	DC.L	0			; checksum will go here
	DC.L	880

Init:	movem.l d0-a6, -(sp)

	lea	CUSTOM, a6
	move.l	#ChipSpace, a4

	move.l	a4, a2			; ChipSpace
	move.l	#SPACE_SIZE, d2
.clear	move.b	#0, (a2)+		; Clear chipspace
	dbf	d2, .clear

	bsr	ResetBpls

	; Draw logo			Screen in a3, Bits in a2
DrawLogo:
	;move.l	a3, a5
	lea	Logo(pc), a2		; logo pixel bits
	add.w	#40*91+2, a3		; starting line
	moveq	#5-1, d0		; y size
.y	moveq	#16-1, d1		; x size
	move.w	(a2)+, d2		; pixel bits

.x	moveq	#0, d3			; outpixel
	;move.w	#%1010101010101010,d3
	btst	#0, d2
	beq.b	.no			; no pixel

	subq	#1, d3			; = move.w #%1111111111111111, d3
	;move.w	#%1111111111111111,d3
.no	moveq	#8-1, d4
	move.l	a3, a1
.cy	move.w	d3, (a1)
	add.w	#80, a1			; advance 2 scanlines
	dbf	d4, .cy
	addq	#2, a3

	asr.w	#1, d2			; next pixel bit
	dbf	d1, .x
	add.w	#488+40, a3
	dbf	d0, .y

	;echosize "Logo:", DrawLogo

	; Init audio channel structures
InitAudio:
	move.l	a6, a5			; CUSTOM
	lea	Sample2(a4), a3
	lea	Channel0(pc), a2	; channel info
	moveq	#4-1, d0		; channel #
.chinit move.l	a3, AUD0LCH(a5)		; sample location
	move.w	(a2)+, AUD0LEN(a5)	; sample length in words
	;move.b	#0,(a2)
	;move.w #$0000+FADESPEED, (a2)+ ; set vol+fade; advance to next chan.
	;move.b #2, AUD0VOL(a5)	; channel volume
	add.w	#$10, a5		; next dest regs
	;add.w	#CHANSIZE, a2		; next channel struct
	dbf	d0, .chinit

	; bass channel
	lea	Sample1(a4), a3
	move.l	a3, AUD3LCH(a6)

;	bset.b	#1, PRA_A		; turn off audio filter

Inited: lea.l	Copper(a4), a1		; set copperlist
	move.l	a1, COP1LCH(a6)

	if	SNARE = 1
	lea	Fadespeeds(pc), a1
	lea	Sample3(a4), a0
	moveq	#64-1, d0
.cpsn	move.w	(a1)+, (a0)+
	dbf	d0, .cpsn
	endc

	move.w	#$4000, INTENA(a6)	; disable interrupts and set some DMAs
	move.w	#$87CF, DMACON(a6)

;	echosize "Init:", Init

Unpack: ; unpack samples
	lea	Sample1RLE(pc), a0
	lea	Sample1(a4), a1
	bsr	UnpackSample

	;lea	Sample2RLE(pc), a0
	;lea	Sample2(a4), a1
	if	ORIGINALMELODY = 0
	lea	Sample1RLE(pc), a0
	endc
	bsr	UnpackSample	; Sample2RLE


; unpack pattern data

	; a0 = notedata (rhythm# byte, run length + periodtab offset nybbles)
	; a1 = pointer to patterndata dest. row

	lea	NotesMelo0_0(pc), a0
	lea	P00_0(a4), a1
	bsr	UnpackPatterns

	rem>	; no need to unpack everything separately
	lea	NotesMelo0_1(pc), a0
	lea	P00_1(a4), a1
	bsr	UnpackPatterns

	lea	NotesMelo0_2(pc), a0
	lea	P00_2(a4), a1
	bsr	UnpackPatterns

	lea	NotesMelo0_3(pc), a0
	lea	P00_3(a4), a1
	bsr	UnpackPatterns
	<erem
	
;	lea	raak(a4), a0
;	move.l	#'FOOX',(a0)

	rem>
	lea	P00_0(a4), a1
	lea	P00_EXT(a4), a2
	move.l	#PATTERNSIZE*2-1, d0
.copy	move.b	(a1)+, (a2)+
	dbf	d0, .copy
	<erem

*******************************************************************************
* Main loop
*******************************************************************************

MainLoop:

.WaitVBL:
	tst.b	VPOSR+1(a6)
	beq.b	.WaitVBL
.wvb:	tst.b	VPOSR+1(a6)
	bne.b	.wvb

	move.w	#$0020, DMACON(a6)	; switch off sprite DMA

	bsr	PlayMusic

	; ---------------------------------------------------------------------

.wait	move.l	VPOSR(a6), d7
	and.l	#$1ff00, d7
	cmp.l	#130<<8, d7
	bmi.b	.wait

	lea	Sample1(a4), a0
	;lea	andtab(pc), a1
	move.b	SongLoops(a4), d1

.d	moveq	#0, d0
	move.b	efxpos(a4), d0
	add.w	d0, a0

	;move.b	d1, d3
	;and.b	#%11, d3		; andtab[0..3]
	;move.b	(a1,d3), d3		; read andtab[] value

.line	move.b	(a0)+, d2
	add.b	row(pc), d2
	and.b	#55, d2		; 51,115,55,119

	btst	#0, d1			; songloops even?
	bne.b	.scroll
.fx2	and.w	d0, d2			; !!!

.scroll	;and.b	d3, d2			; limit scrollpos by andtab[]
	move.b	d2, BPLCON1+1(a6)	; horizontal scroll

	;add.w	#187, d0
	;add.b	efxpos(a4), d0
	;add.b	row(pc), d0
	;and.w	#%111111111, d0
	;lsl.w	#1, d0

	;add.b	d2, d0
	;and.w	#%111111, d0
	;.wait2	dbf	d2, .wait2

	move.l	VPOSR(a6), d7
	and.l	#$1ff00, d7
	cmp.l	#210<<8, d7
	bpl.b	MainLoop

	; ---------------------------------------------------------------------

	btst.b	#6, PRA_A		; loop until mouse clicked
	bne.b	.line


Exit:	move.w	#$000F, DMACON(a6)	; turn off sound
	movem.l (sp)+, d0-a6		; restore registers

	if	RELEASE = 0		; assembled as executable
	rts
	endc

	move.l	IVBLIT(a6), a0		; restore copper
	move.l	gb_copinit(a0), CUSTOM+COP1LCH

	; Return init function of dos.library resident in A0
	lea	DosName(pc), a1
	jsr	_LVOFindResident(a6)
	move.l	d0, a0
	move.l	RT_INIT(a0), a0
	moveq	#0, d0

	if	RELEASE = 1
	rts
	endc

DosName	dc.b	'dos.library',0

*******************************************************************************
* Music playroutine
*******************************************************************************

PlayMusic:
	moveq	#0, d0
	lea	ticks(a4), a1
	move.b	(a1), d0
	;tst.b	d0			; enough ticks passed?
	bne	.fade			; no
	move.b	#SONGTICKS, (a1)	; yes; reset tick counter and advance

	lea	row(pc), a1
	move.b	(a1), d0
	addq.w	#1, d0			; advance to next row in pattern

	cmp.b	#PATTERNSIZE/2, d0
	beq	.half
	cmp.w	#PATTERNSIZE, d0	; pattern played through?
	bne.b	.rok			; not yet
.restart
	lea	SongLoops(a4), a3
	add.b	#1, (a3)
	cmp.b	#MAXSONGLOOPS, (a3)	; reset fx when looped this many times
	bne.b	.nore
	push	a1
	bsr	ResetBpls		; reset bpls and SongLoops
	pop	a1

.nore	add.w	#40, Copper+BPLPTRS+6+8(a4) ; offset bitplane
	moveq	#0, d0			; loop to pattern start

.rok	move.b	d0, (a1)		; store row number

	lea	P00_0(a4), a1		; patterndata
	moveq	#4-1, d4		; channel #
	lea	Channel0(pc), a0
	move.l	a6, a5			; CUSTOM
.chan	moveq	#0, d1
	move.b	(a1,d0), d1		; note := patterndata[row]
	;tst.b	d1
	beq.b	.next			; skip if note=0
;	cmp.b	#CC, d1
;	beq.b	.domaxvol		; special: reset bass vol to 64

	if	SNARE = 1

	tst.b	d4	; bass channel?
	bne.b	.putp	; no

	cmp.b	#2, d1	; snare hit?
	bne.b	.nosn	; no

.snare	move.b	#11, d1
	move.b	#1, .issnare
	lea	Sample3(a4), a3
;	lea	ChannelS(pc), a2	; channel info
	bra.b	.psn
.nosn	cmp.b	#1, .issnare
	bne	.putp
	move.b	#0, .issnare
	lea	Sample1(a4), a3
;	lea	Channel3(pc), a2	; channel info
.psn	move.l	a3, AUD0LCH(a5)		; sample location
;	move.w	(a2), AUD0LEN(a5)	; sample length in words
	;move.w	#CHANNELS_ON, DMACON(a6)

	endc

.putp	lea	Periods(pc), a2
	subq	#1, d1			; 0-based periodtable access
	add.b	d1, d1
	move.w	(a2,d1), AUD0PER(a5)	; periodtable[note]

	rem>
	move.w	(a2,d1), d7
	move.b	efxpos(a4), d6
	and.b	#%111, d6
	add.b	d6, d7
	move.w	d7, AUD0PER(a5)	; periodtable[note]
	<erem

;	lea	wantfade(pc), a3
;	cmp.b	#1,(a3)
;	bne.b	.maxvol

	lea	Fadespeeds(pc), a3
	cmp.b	#1, wantfade
	bne	.faden
.fadey	move.b	(a3,d4.w), S_FADEY(a0)
	bra.b	.maxvol
.faden	move.b	(a3,d4.w), S_FADEN(a0)

.maxvol	lea	Chanvols(pc), a3
	move.b	(a3,d4.w), d2
.pv	move.b	d2, S_VOL(a0)
	move.w	d2, AUD0VOL(a5)

.next	addq	#CHANSIZE, a0		; next channel's info
	add.w	#PATTERNSIZE, a1	; next channel's pattern data
.last	add.w	#$10, a5		; next channel's Paula regs
	dbf	d4, .chan		; process next channel

.out	if	NOAUDIO = 0
	move.w	#CHANNELS_ON, DMACON(a6)
	endc

.efx	lea	efxpos(a4), a5
	lea	Sample1(a4), a3
	move.b	(a5), d0		; dest. pos in sample data
	and.w	#64-1, d0		; sample is 128 bytes long, don't go past it
	add.w	d0, a3
	not.b	(a3)			; simulate ProTracker EFx effect
	lea	Sample2(a4), a3
;	and.b	#32-5, d0		; sample is 32 bytes long, don't go past it
	and.b	#32-9, d0		; sample is 32 bytes long, don't go past it
	not.b	(a3,d0)			; simulate ProTracker EFx effect
	add.b	#1, (a5)
	rts

.half:	cmp.b	#1, wantfade
	beq	.kih
	move.b	#1, wantfade
	lea	Chanvols(pc),a0
	move.l	#$402A2A2A, (a0)
	bra.b	.nore
.kih	sub.w	#40, Copper+BPLPTRS+6(a4) ; offset other bitplane
	bra.b	.rok

.domaxvol:
	lea	Channel3+1(pc), a1
	move.b	#5, (a1)
	bra.b	.maxvol

.fade:	lea	Channel0(pc), a3
	move.l	a6, a5
	moveq	#0, d4			; channel #
	moveq	#0, d2
.fac	move.b	S_VOL(a3), d2		; current channel volume

	cmp.b	#1, wantfade
	bne	.faden2
.fadey2	sub.b	S_FADEY(a3), d2		; subtract fade speed
	bra.b	.fad
.faden2	sub.b	S_FADEN(a3), d2		; subtract fade speed
.fad	bcs.b	.nxt			; stop fading
	move.b	d2, S_VOL(a3)
	if	NOAUDIO = 1
	moveq	#0, d2
	endc
	move.w	d2, AUD0VOL(a5)

.nxt	addq	#CHANSIZE, a3
	add.w	#$10, a5
	addq	#1, d4
	cmp.b	#4, d4
	bne.b	.fac
	subq	#1, d0
	move.b	d0, (a1)		; ticks++
	bra.b	.efx

	echosize "Playroutine:", PlayMusic ;+32+36+2+8+71

	if	SNARE = 1
.issnare: dc.b 0
	endc
wantfade: dc.b NOINTROPATTERN
	even
; -----------------------------------------------------------------------------

SetBPLPtrs:	; a0 = bitmap, a1 = location of BPLxPTH in copperlist
	move.l	a0, d0
	move.w	d0, 4(a1)
	swap	d0
	move.w	d0, (a1)
	rts

ResetBpls:
	move.b	#0, SongLoops(a4)

	; set bitplane pointers
	lea	Screen+(SCREENWIDTH/8*YPADDING)(a4), a0
	move.l	a0, a3			; store screen loc for future use
	;subq	#1, a0
	lea	bpl1+2(pc), a1
	bsr.b	SetBPLPtrs
	addq	#8, a1
	sub.w	#40, a0
	bsr.b	SetBPLPtrs

	; copy copperlist to chip
	lea	CopperData(pc), a0
	lea	Copper(a4), a1
	moveq	#(CopperData_End-CopperData)/4-1, d6
.copy	move.l	(a0)+, (a1)+
	dbf	d6, .copy
	rts

; -----------------------------------------------------------------------------
; a0 = rle-packed sample data
; a1 = destination
;
UnpackSample:
.copy	move.b	(a0)+, d0	; value
	cmp.b	#ENDBYTE, d0
	beq.b	.done
.rle	moveq	#0, d1
	move.b	(a0)+, d1	; length
.fill	move.b	d0, (a1)+
	dbf	d1, .fill
	bra.b	.copy
.done	rts

; -------------------------------------------------------------------
; Unpacks a 32-row pattern
;
; a0 = notedata (rhythm# byte, run length + periodtab offset nybbles)
; a1 = pointer to patterndata dest. row

UnpackPatterns:
	moveq	#4*8-1, d6
.yeet	moveq	#0, d5
	move.b	(a0)+, d5	; rhythm #
	mulu.w	#4, d5		; 4 bytes per rhythm
	lea	Rhythms, a2	; ptr to rhythm bitpatterns
	move.l	(a2,d5.w), d4	; get rhythm pattern

	moveq	#0, d3		; run length counter
	moveq	#32-1, d2	; row counter
.row	rol.l	#1, d4		; get next bit in rhythm pattern
	btst	#0, d4		; bit set?
	beq.b	.loop		; no, don't put new byte

	tst.b	d3		; need to read next byte?
	bne.b	.put		; no

.read	move.b	(a0), d3	; get run length + note#
	move.w	d3, d5
	and.b	#%1111, d5	; d5 = note#
	addq	#1, d5
	lsr.b	#4, d3		; d3 = run length
	and.b	#%1111, d3	; req?
	tst.b	d3		; or are we done with this pattern?
	beq.b	.fff		; yes

	addq	#1, a0

	cmp.b	#$F, d3
	bne.b	 .put
	addq	#1, d3

.put	move.b	d5, (a1)
	subq	#1, d3

.loop	addq	#1, a1
	dbf	d2, .row

.fff	dbf	d6, .yeet
	rts

*******************************************************************************
* Data
*******************************************************************************

DataStart:

YPOS = 0

COLMOD   = 0
COLMODBG = 0

	if ALTPALETTE = 1
BG     = $BBA
BGDARK = $CB9
	else
BG     = $324
BGDARK = $325
	endif

CopperData:	dc.w	FMODE,	 $0	; AGA slow fetch mode
		dc.w	DIWSTRT, $2C81	; Display window start (top left)
		dc.w	DIWSTOP, $2CB1	; Display window stop (bottom right)
		dc.w	DDFSTRT, $38	; Display data fetch start (horiz.)
		dc.w	DDFSTOP, $D0	; Display data fetch stop  (horiz.)
		dc.w	BPLCON1, $0
		dc.w	BPLCON2, $0
		dc.w	BPL1MOD, $0
		dc.w	BPL2MOD, $0

bpl1:		dc.w	BPL1PTH, 0, BPL1PTL, 0	; Bitplane pointers
bpl2:		dc.w	BPL2PTH, 0, BPL2PTL, 0	;
		dc.w	BPLCON0, PLANES_2|COLOR ; Screen on

		COPPER_SETCOLOR	0, BG+COLMODBG		; bar

		if ALTPALETTE = 1
		COPPER_SETCOLOR	1, $9AB+COLMOD ;$9AE
		COPPER_SETCOLOR	2, $B86+COLMOD ;$BA5
		COPPER_SETCOLOR	3, $784+COLMOD ;$F00
		else
		COPPER_SETCOLOR 1, $424+COLMOD	; ->
		COPPER_SETCOLOR 2, $334+COLMOD	; <-
		COPPER_SETCOLOR 3, $327+COLMOD 	; text 327
		endif

		COPPER_WAIT	240-YPOS-70,7

		COPPER_SETCOLOR 0, BGDARK+COLMODBG	; background

		;dc.w BPLCON0, 	$0200		; Screen off
		COPPER_END

CopperData_End:
		echosize "Copper:", CopperData

Chanvols:	dc.b	VOLUME_BASS,VOLUME_MELODY,VOLUME_MELODY,VOLUME_MELODY

Channel0:	dc.b	00	; volume
		dc.b	16	; length/fade speed
Channel1:	dc.b	00	; volume
		dc.b	16	; length/fade speed
Channel2:	dc.b	00	; volume
		dc.b	16	; length/fade speed
Channel3:	dc.b	00	; volume
ch3fade:	dc.b	64	; length/fade speed

		; 51,115,55,119
;andtab:	dc.b	%01110011, %01110111, %00110111, %00110011

		; VOID logo
Logo:		dc.w	%0111010111101001
		dc.w	%1101000100101001
		dc.w	%1001010100101001
		dc.w	%1001010100101011
		dc.w	%1111010111101110

		; Period table, 32 bytes
Periods:	dc.w	604,570,538,508,480,453,428,404 ; 0..7
		dc.w	381,360,339,320,302,285,269,254 ; 8..F

; On which rows to insert notedata (max 16 per rhythm!)
;
Rhythms:	dc.l	%10101010101010101010101010101010 ; bass P.1+2
		dc.l	%10001000100100100010010010010010 ; melody P.#1
		dc.l	%10001000100100100010000010001000 ; melody P.#2

row:		dc.b	PATTERNSIZE-1

; ================================ SAMPLE DATA ===============================

		; Sample 01 (64 words, 128 bytes): "bass"
Sample1RLE:	dc.b	127,83,115,2,100,6,077,4,055,4
		dc.b	039,3,020,3,12,4,250,10,ENDBYTE

		if	ORIGINALMELODY = 1
		; Sample 02 (16 words, 32 bytes): "melody"
Sample2RLE:	dc.b	17,3,238,5,38,3,191,6,43,4,225,5,ENDBYTE
		endc
		;echosize "Samples:", Sample1RLE

; =============================== PATTERN DATA ===============================

SN=$11

; Following notedata is rhythm# + run length + periodtab offset nybbles

Fadespeeds:	dc.b	FADESPEED_BASS,FADESPEED,FADESPEED,FADESPEED
NotesMelo0_0:	dc.b	01, $2B,$2C,$3B,$2C,$1B, 01, $29,$2A,$39,$2A,$19
		dc.b	01, $2B,$2C,$3B,$2C,$1B, 01, $29,$2A,$39,$2A,$19
NotesMelo1_0:	dc.b	02, $2D,$2E,$4D, 02, $2D,$2E,$4D
		dc.b	02, $2D,$2E,$4D, 02, $2D,$2E,$4D
NotesMelo0_1:	dc.b	01, $A7, 01, $A5
		dc.b	01, $A7, 01, $A5
NotesMelo1_1:	dc.b	02, $89, 02, $49,$48
		dc.b	02, $89, 02, $49,$48
NotesMelo0_2:	dc.b	01, $A4, 01, $52,$50
		dc.b	01, $A4, 01, $52,$50
NotesMelo1_2:	dc.b	02, $86, 02, $84
		dc.b	02, $86, 02, $84
NotesMelo0_3:	if	SNARE = 1
		dc.b	00, $24, SN, $34, SN, $34, SN, $34, SN, $14
		dc.b	00, $22, SN, $32, SN, $12, $29, SN, $39, SN, $19
		dc.b	00, $24, SN, $34, SN, $34, SN, $34, SN, $14
		dc.b	00, $22, SN, $32, SN, $12, $29, SN, $39, SN, $19
		else
		dc.b	00, $F4, 00, $F2     ; bass
		dc.b	00, $F4, 00, $82,$89 ; bass
		endc
NotesMelo1_3:	dc.b	00, $FE, 00, $89,$8D ; bass
		dc.b	00, $FE, 00, $89,$88 ; bass

; ============================================================================

		echosize "Song: ", Chanvols

BB_END:
BB_SIZE = BB_END-BB_START

	;echo "------------"
	echosize "Data:", Channel0
	echosize "Total:", BB_START

	echo "Remaining:"
	printv 1024-BB_SIZE

	if	RELEASE = 1
	if	BB_SIZE<1024
	ds.b	1024 - BB_SIZE, "B"
	endc
	endc

*******************************************************************************
; Put everything that needs to be in chip memory in this section!

Space:		rsreset

Copper:		rs.b	CopperData_End-CopperData

BPLPTRS = bpl1-CopperData

Sample1:	rs.w	64
Sample2:	rs.w	64 ;16
	if	SNARE = 1
Sample3:	rs.w	64
	endc

raak:		rs.b	4

P00_0:		rs.b	PATTERNSIZE
P00_1:		rs.b	PATTERNSIZE
P00_2:		rs.b	PATTERNSIZE
P00_3:		rs.b	PATTERNSIZE

efxpos:		rs.b	1
SongLoops:	rs.b	1

ticks:		rs.b	1
foo:		rs.b	1 ; unused

Screen:		rs.b	SCREENWIDTH/8*(SCREENHEIGHT+(YPADDING*2))

SPACE_SIZE:	rs.l	0

ChipSpace = $7FF00-SPACE_SIZE

	rem>
	echo
	echo "SPACE_SIZE="
	printv SPACE_SIZE
	echo
	echo "ChipSpace="
	printv ChipSpace
	<erem

; -----------------------------------------------------------------------------
